plugins {
    id 'java'
    id 'com.github.node-gradle.node' version '3.2.1'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def projectOutput = "$buildDir/generated-resources/"

node {
    version = '22.6.0'
    npmVersion = '10.8.2'
    download = true
    nodeProjectDir = file("$projectDir")
}

task installDependencies(type: NpmTask) {
    args = ['install']
}

task webpack(type: NpmTask) {
    args = ['run', 'build']
    dependsOn installDependencies
    
    inputs.files(project.fileTree(".").matching {
        exclude("**/node_modules/**", "**/dist/**", "**/.awcache/**", "**/yarn-error.log")
    }.toList())

    outputs.files(fileTree(projectOutput))
}

processResources {
    dependsOn webpack
}

clean {
    delete 'dist'
}

task deepClean {
    doLast {
        delete(file(".gradle"))
        delete(file("node_modules"))
    }

    dependsOn clean
}

project(':gateway')?.tasks?.named('processResources')?.configure {
    dependsOn webpack
}

sourceSets {
    main {
        resources {
            srcDir projectOutput
        }
    }
}
